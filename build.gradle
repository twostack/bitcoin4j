plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'signing'
}

group 'org.twostack'
version '1.4.1-SNAPSHOT'

repositories {
    mavenCentral()
}

compileJava {
    options.encoding = "UTF-8"
}

javadoc {
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    api 'org.bouncycastle:bcprov-jdk15to18:1.68'
    api 'com.google.guava:guava:30.0-android'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'net.jcip:jcip-annotations:1.0'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.easymock:easymock:3.2'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.5.2'
    testImplementation 'org.slf4j:slf4j-jdk14:1.7.30'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.19.0'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
}


publishing {

    repositories {
        maven {
            def releaseRepoUrl = "https://s01.oss.sonatype.org/content/repositories/releases/"
            def snapshotRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"

            url = version.endsWith('SNAPSHOT') ? snapshotRepoUrl : releaseRepoUrl
            name = "OSSRH"
            credentials {
                username = findProperty('ossrhUsername') ? ossrhUsername : ''
                password = findProperty('ossrhPassword') ? ossrhPassword : ''
            }
        }

    }
    publications {
        maven(MavenPublication) {
            groupId = 'org.twostack'
            artifactId = 'bitcoin4j'
            version = version

            from components.java

            pom {

                name = 'Bitcoin4J'
                packaging = 'jar'
                description = 'A native Java Bitcoin library. Specifically targeted at BitcoinSV (BSV).'
                url = 'https://twostack.org'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'stephanfeb'
                        name = 'Stephan M. February'
                        email = 'stephan@twostack.org'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/twostack/bitcoin4j.git'
                    developerConnection = 'scm:git:ssh://github.com:twostack/bitcoin4j.git'
                    url = 'https://github.com/twostack/bitcoin4j'
                }
            }
        }

    }
}

signing {
    sign publishing.publications.maven
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}


